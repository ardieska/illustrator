layout ZigZagUI
{
    view subview(identifier: "AIEveSubView",
                 layout_theme: @dialog_large,
                 placement: @place_column,
                 show_border: false,spacing:@unrelated_spacing)
    {
        column(child_horizontal: @align_fill, spacing: @inter_group_spacing)
        {
            group(child_horizontal: @align_fill,
                  identifier: "RoughenOptionsGrp",
                  name: "$$$/ZigZag/CNTL/128=Options")
            {
                combo_slider(identifier: "SizeSlider",
						   slider_point: @up,
						   name: "$$$/ZigZag/Dlg/ZigZag/4=Si&ze:",
                            decimal_places: 2,
							digits: 8,
                            set_default_focus: true
                       );

                radiogroup(identifier: "RoughenOptionsRadioGrp", placement: @place_row)
                {
                    radio_button(bind: @SizeRadioGp,
                                 identifier: "RelativeRadioButton",
                                 name: "$$$/ZigZag/Dlg/ZigZag/RelativeRadio=&Relative",
                                 value: @one);

                    radio_button(bind: @SizeRadioGp,
                                 identifier: "AbsoluteRadioButton",
                                 name: "$$$/ZigZag/Dlg/ZigZag/AbsoluteRadio=&Absolute",
                                 value: @two);
                }

                combo_slider(identifier: "RidgeSlider",
                           slider_point: @up,
                           name: "$$$/ZigZag/Dlg/ZigZag/10=Ri&dges per segment:",
						   digits: 8,
                           decimal_places: 2);
            }

            group(identifier: "RoughenPointsGrp",
                  name: "$$$/ZigZag/CNTL/129=Points")
            {
                radiogroup(identifier: "RoughenPointsRadioGrp", placement: @place_row)
                {
                    radio_button(bind: @PointsRadioGp,
                                 identifier: "SmoothRadioButton",
                                 name: "$$$/ZigZag/Dlg/ZigZag/SmoothRadio=&Smooth",
                                 value: @one);

                    radio_button(bind: @PointsRadioGp,
                                 identifier: "CornerRadioButton",
                                 name: "$$$/ZigZag/Dlg/ZigZag/CornerRadio=&Corner",
                                 value: @two);
                }
            }
        }

        row(child_vertical: @align_center, horizontal: @align_fill)
        {
            checkbox(identifier: "previewButton",
                     name: "$$$/ZigZag/Dlg/ZigZag/Preview=&Preview",
                     guide_mask: [@guide_label]);

			row(child_horizontal: @align_right, horizontal: @align_fill, reverse: os == @mac, spacing: @related_spacing)
			{
				button( identifier: "okButton",
							   name: "$$$/gOK=OK",
							   commit_button: true);

				button( identifier: "cancelButton",
					   name: "$$$/gCancel=Cancel",
					   commit_button: true);
			}

        }
    }
}
