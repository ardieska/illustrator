layout PerspectiveUI
{
    view subview(identifier: "AIEveSubView",
                 layout_theme: @dialog_large,
                 placement: @place_column,
                 show_border: false,
				 spacing: @unrelated_spacing)
    {
        column(horizontal: @align_fill, child_horizontal: @align_fill)
        {
        row(horizontal: @align_left)
		{
		overlay()
        {
		 edit_text(	identifier: "PName",
					horizontal:@align_fill,
                    name: "$$$/PerspectiveUI/PresetDlg/AutoOptions/Name=&Name:",
				max_characters:255
                   );
          popup(identifier: "PresetPopup",
				include_custom_item: false,
				items: [ 
                                   { name: "$$$/PerspectiveUI/PresetDlg/Preset/ToResizeNew=Perspective Grid Default Preset 99",
                                     value: 0} ],
                     name: "$$$/PerspectiveUI/PresetDlg/typename/PresetType=&Preset:",
                     horizontal: @align_fill);
           }
            
            button(identifier: "saveButton",
            	image: {normal: "P_SaveGradient_Sm_N.png", dark_normal: "P_SaveGradient_Sm_D.png"}
                   );      
         }

            column(child_horizontal: @align_fill, horizontal: @align_fill,spacing: @inter_group_spacing)
            {
                group(identifier: "PGridSettings",
                      name: "$$$/PerspectiveUI/PresetDlg/GridSettingGroup=Perspective Grid Settings")
                {
                    popup(identifier: "PresetTypePopup",
                          include_custom_item: false, horizontal: @align_fill,
                          items: [ 
                                   { name: "$$$/PerspectiveUI/PresetDlg/type/OnePoint=One Point Perspective",
                                     value: 0},
                                   { name: "$$$/PerspectiveUI/PresetDlg/type/TwoPoint=Two Point Perspective",
                                     value: 1 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/type/ThreePoint=Three Point Perspective",
                                     value: 2 } ],
                          name: "$$$/PerspectiveUI/PresetDlg/typename/Type=&Type:");

                    popup(identifier: "PresetUnitPopup",
                          include_custom_item: false, horizontal: @align_fill,
                          items: [ 
                                   { name: "$$$/PerspectiveUI/PresetDlg/Units/CM=Centimeters",
                                     value: "CM" },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Units/Inches=Inches",
                                     value: "Inches" },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Units/Pixels=Pixels",
                                     value: "Pixels" },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Units/Points=Points",
                                     value: "Points" } ],
                          name: "$$$/PerspectiveUI/PresetDlg/Units/UnitsName=&Units:");

                    popup(identifier: "PresetScalePopup",
                          include_custom_item: false, horizontal: @align_fill,
                          items: [ 
								   { name: "$$$/PerspectiveUI/PresetDlg/Scale/CustomPlace=Custom",
                                     value: 0 },
								   { name: "",
                                     value: 1 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/hundred=1:100",
                                     value: 2 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/fifty=1:50",
                                     value: 3 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/twenty=1:20",
                                     value: 4 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/ten=1:10",
                                     value: 5 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/five=1:5",
                                     value: 6 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/one=1:1",
                                     value: 7 },
								   { name: "",
                                     value: 8 },
                                   { name: "$$$/PerspectiveUI/PresetDlg/Scale/CustomScale=Custom...",
                                     value: 9 } ],
                          name: "$$$/PerspectiveUI/PresetDlg/Scale/ScaleText=&Scale:");

                    unit_hot_number(identifier: "GENumberEdit",
                                name: "$$$/PerspectiveUI/PresetDlg/GridLineEveryNumberEdit=&Gridline every:",
                                decimal_places: 2, digits: 7
                                );


					
                    row(child_vertical:@align_center)
                    {
						image(identifier: "WarningIcon",
								image: {normal:"SP_Info_Sm_N.png",dark_normal:"SP_Info_Sm_N_D.png"},guide_mask: [ @guide_label ]);
				
                        static_text(
                                    identifier: "InfoString",
                                    name: "$$$/PerspectiveUI/PresetDlg/ScaleInfoText=At True Height Line, XXXX cm on the Artboard represents XXXX cm.",
									theme_font_size: @small);
                    }

                    separator();
                   

                    row()
                    {
					unit_hot_number(identifier: "ViewAngleNumberAngleEdit",
                                    name: "$$$/PerspectiveUI/PresetDlg/ViewAngleNumberEdit=Viewing &Angle:",
                                    decimal_places: 2, digits: 7,
                                    unit:"NumberEditUnit_Degree"
                                    );
                    }

                    unit_hot_number(identifier: "ViewDistanceNumberEdit",
                                name: "$$$/PerspectiveUI/PresetDlg/ViewDistanceNumberEdit=Viewing &Distance:",
                                decimal_places: 2, digits: 7, indent: 20
                                );

					unit_hot_number(identifier: "HorizonHeightNumberEdit",
                                name: "$$$/PerspectiveUI/PresetDlg/HorizonHtNumberEdit=Hori&zon Height:",
                                decimal_places: 2, digits: 7
                                );

			
				row(child_vertical:@align_center,guide_mask: [ @guide_label ])
				{
						static_text(identifier: "ThirdVPXStaticText",
                                    name: "$$$/PerspectiveUI/PresetDlg/ThirdVPXStaticText=Third Vanishing Point:");

                        unit_hot_number(identifier: "ThirdVPXNumberEdit",
                                    name: "$$$/PerspectiveUI/PresetDlg/ThirdVPXNumberEdit=&X:",
                                    decimal_places: 2, digits: 7
                                   );

                        unit_hot_number(identifier: "ThirdVPYNumberEdit",
                                    name: "$$$/PerspectiveUI/PresetDlg/ThirdVPYNumberEdit=&Y:",
                                    decimal_places: 2, digits: 7
                                    );
				}
				
                }

                group(identifier: "GridColor",
                      name: "$$$/PerspectiveUI/PresetDlg/GroupGridColor=Grid Color & Opacity", child_horizontal: @align_fill, horizontal: @align_fill)
                {
                    row(child_vertical: @align_center)
                    {
						popup(identifier: "PresetLgridPopup",
							  include_custom_item: false,
							  items: gLargeColorPopupList,
							  bind: @lgridPopup,
							  name: "$$$/PerspectiveUI/PresetDlg/AutoOptions/LGrid=&Left Grid:");


						swatch(identifier: "SelectedLGridColorImagePlaceholder",
								border: @border_normal);
                    }

                    row(child_vertical: @align_center)
                    {
						 popup(identifier: "PresetsRgridPopup",
							  include_custom_item: false,
							  items: gLargeColorPopupList,
							  bind: @rgridPopup,
							  name: "$$$/PerspectiveUI/PresetDlg/AutoOptions/rGrid=&Right Grid:");
			 
						swatch(identifier: "SelectedRGridColorImagePlaceholder", 
								border: @border_normal);

                    }

                    row(child_vertical: @align_center)
                    {
						 popup(identifier: "PresetsHgridPopup",
							  include_custom_item: false,
							  items: gLargeColorPopupList,
							  bind: @hgridPopup,
							  name: "$$$/PerspectiveUI/PresetDlg/AutoOptions/HGrid=&Horizontal Grid:");
		
						swatch(identifier: "SelectedHGridColorImagePlaceholder",
							border: @border_normal);
                    }
                    
                    
			combo_slider(identifier: "PresetsGridOpacityEdit",
                              	name: "$$$/PerspectiveUI/PresetDlg/AutoOptions/LGridOpacity=&Opacity:",
                              	decimal_places: 0,
				slider_point: @up,
				min_max_filter: @filterOpacity,
				minLength: 130,
                              	unit: "NumberEditUnit_Percent", digits: 3);
					
                }
            }
        }

        row(horizontal: @align_right, reverse: os == @mac, spacing: @related_spacing)
        {
            button(identifier: "okButton", name: "$$$/gOK=OK", commit_button: true);

            button(identifier: "cancelButton", name: "$$$/gCancel=Cancel", commit_button: true);

        }
    }
}