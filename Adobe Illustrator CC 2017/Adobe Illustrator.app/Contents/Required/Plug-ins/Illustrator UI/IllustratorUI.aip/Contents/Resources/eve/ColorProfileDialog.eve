layout ColorProfileDialog
{
 view subview(layout_theme : @dialog_large,
				placement: @place_column,
				show_border: false, spacing: @unrelated_spacing
                )
    {
        column(child_horizontal: @align_fill)
        {
			optional(margin:0, bind: @assignProfileDialog, value: false)
			{
				row()
				{
					column()
					{
						info_icon(icontype: "warning", size: "small");
					}
					optional(margin: 0, bind: @showRGBOrCMYKWarning, value:0)
					{
						static_text(identifier: "missingRGBProfile",
						name: "$$$/_ColorConvdg/Dlg/OpenNoProfileDlgRGB/10=The document does not have an embedded RGB profile.");
					}
					optional(margin: 0, bind: @showRGBOrCMYKWarning, value:1)
					{
						static_text(identifier: "missingCMYKProfile",
						name: "$$$/_ColorConvdg/Dlg/OpenNoProfileDlgCMYK/10=The document does not have an embedded CMYK profile.");
					}
					
				}	
			}
			
       
        group(identifier: "optionsFrame",guide_mask:[@guide_label],
					name: @dialogGroupName)
		{
				radiogroup()
				{
					radio_button(bind: @radiobuttonIndex, identifier: "DontManage", name: @firstOption,  value: 0);
					row(spacing: @label_widget_spacing)
					{
						radio_button(bind: @radiobuttonIndex, identifier: "Working", name: @secondOption,  value: 1);
						static_text(identifier: "currentProf", name: @workingProfName);
					}
					row(spacing: @label_widget_spacing)
					{
						radio_button(bind: @radiobuttonIndex, identifier: "ChooseProfile", name: @thirdOption,  value: 2);
						popup(identifier: "ProfilePopup", include_custom_item: false, popup_maximum_width_chars: 20, popup_minimum_width_chars: 20);
					}
				}
		}
	 } 
		row(child_vertical: @align_center, horizontal: @align_right, reverse: os == @mac, guide_mask:[@guide_label], spacing: @related_spacing)
		{
			button(identifier: "okButton", set_default_focus: true, name: "$$$/gOK=OK", commit_button: true);
			button(identifier: "cancelButton", name: "$$$/gCancel=Cancel", commit_button: true);
		}
	
           				
     }	
                
}