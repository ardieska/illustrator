layout ReflectDialog
{
    view subview(show_border: false,
                 layout_theme: @dialog_large,
                 identifier: "ReflectDialogSubView",spacing:@unrelated_spacing,
                 child_horizontal: @align_fill)
    {    
	column(child_horizontal: @align_fill, spacing: @inter_group_spacing)
	{                   
		group(name: "$$$/_TRANSFOR/Dlg/Reflect/reflectAxisGroupbox=Axis",
			  guide_mask: [ @guide_label])
		{
			radiogroup()
			{
				row(child_vertical: @align_center)
				{
					radio_button(name: "$$$/_TRANSFOR/Dlg/Reflect/horizontalReflect=&Horizontal",
								 identifier: "HorizontalRadioButton",
								 bind: @AxisRadioGp,
								 value: 0);
								 
					image(identifier: "ReflectHorizontalImage",
							image:{normal: "SP_ReflectVertical_Lg_N.png", dark_normal: "SP_ReflectVertical_Lg_N_D.png"});
				}
			
				row(child_vertical: @align_center)
				{
					radio_button(name: "$$$/_TRANSFOR/Dlg/Reflect/verticalReflect=&Vertical",
								 identifier: "VerticalRadioButton",
								 bind: @AxisRadioGp,
								 value: 1);
								 
					image(identifier: "ReflectVerticalImage",
							 image: {normal: "SP_Reflect_Lg_N.png", dark_normal: "SP_Reflect_Lg_N_D.png"});
				}
						 
				row(child_vertical: @align_center, spacing: @label_widget_spacing)
				{
					radio_button(name: "$$$/_TRANSFOR/Dlg/Reflect/angleReflect=&Angle:",
								 bind: @AxisRadioGp,
								 identifier: "AngleRadioButton",
								 value: 2); 
				
					angle_picker(identifier: "AnglePicker",	
								value_range: {min_value:-360, max_value: 360},
								bind: @AngleNumberEditCell,
								show_editbox: true);
				}
			}
		 }
		 

		group(name: "$$$/_TRANSFOR/Dlg/Reflect/reflectOptionsGroupbox=Options",
				 guide_mask: [ @guide_label],
				placement: @place_row)
		{
			checkbox(name: "$$$/_TRANSFOR/Dlg/Reflect/doObjectsReflect=Transform &Objects",
					identifier: "objectsCheckBox",
					bind: @ObjectCheckBoxCell);
			
			checkbox(name: "$$$/_TRANSFOR/Dlg/Reflect/doPatternsReflect=Transform Pa&tterns",
					bind: @PatternCheckBoxCell,
					identifier: "PatternCheckBox");
		}
		row(spacing: @separated_spacing,child_vertical: @align_center)
		{
			checkbox(name: "$$$/_TRANSFOR/Dlg/Reflect/reflectPreview=&Preview",
						bind: @PreviewCheckBoxCell,
						identifier: "PreviewCheckbox",
						guide_mask: [ @guide_label]);
       	}

	}
	row(spacing: @button_group_spacing)
	{
		button(name: "$$$/_TRANSFOR/Dlg/Reflect/copyReflect=&Copy",
					identifier: "copyButton",
					action: @copy,
					horizontal: @align_left,
					commit_button: true,
					vertical: @align_fill);
					
				row(reverse: os == @mac, spacing: @related_spacing,horizontal: @align_right)
				{
					button(name: "$$$/gOK=OK",
							identifier: "okButton",
							action: @ok,
							commit_button: true);

					button(identifier: "cancelButton",
							name: "$$$/gCancel=Cancel",
							action: @cancel,
							commit_button: true);
				}
	}
    }
}
