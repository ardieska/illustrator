layout ObjectEffectRasterizeUI
{
	view subview(identifier: "AIEveSubView",
					placement: @place_column,
					show_border: false,
					layout_theme: @dialog_large,
					spacing: @unrelated_spacing
					)
    {
		column()
		{
			popup(identifier: "ColorModelPopup",
						name: "$$$/Rasterize/Dlg/AutoOptions/TextModel=&Color Model:",
						include_custom_item: false,
						popup_maximum_width_chars: 17, popup_minimum_width_chars: 17,
						bind: @ColorPopupVal, items: @ColorItemSet,
						set_default_focus: true);
			column(child_horizontal: @align_fill)
			{		
				row(horizontal: @align_fill, child_horizontal: @align_fill)
				{
					popup(identifier: "ResolutionPopup", include_custom_item: false, bind: @ResolutionPopup,
						  name: "$$$/Rasterize/Dlg/AutoOptions/GroupResolution=&Resolution:",
						  popup_maximum_width_chars: 17, popup_minimum_width_chars: 17,
						  items: [{ name: "$$$/Rasterize/Dlg/AutoOptions/RadioScreen=Screen (72 ppi)", value: 72 },
							{ name: "$$$/Rasterize/Dlg/AutoOptions/RadioMedium=Medium (150 ppi)", value: 150 },
							{ name: "$$$/Rasterize/Dlg/AutoOptions/RadioHigh=High (300 ppi)", value: 300 },
							{ name: "$$$/Rasterize/Dlg/Options/UseEffectsSetting=Use Document Raster Effects Resolution", value: -2},
							{ name: "", value: 0},
							{ name: "$$$/Rasterize/Dlg/AutoOptions/RadioOther=Other", value: -1 }
							]);
				
					panel(bind: @ResolutionPopup, value: -1)
					{
						unit_hot_number(identifier: "ResolutionNumberEdit",
										min_value: 1,
										max_value: 2400,
										decimal_places: 2,
										digits: 4,
										suffix: "$$$/Rasterize/Dlg/AutoOptions/TextResolution=ppi",
										bind: @OtherResolution,
										guide_mask: [@guide_label]);
					}				
				}
			
				column(horizontal: @align_fill, child_horizontal: @align_fill,spacing: @inter_group_spacing)
				{
					group(name: "$$$/Rasterize/Dlg/AutoOptions/GroupBackground=Background",
							identifier: "BackgroundOptions")
					{
						column(guide_mask: [ @guide_label ])
						{
							radiogroup(placement: @place_column, horizontal: @align_fill, show_border: false, margin: 0)			
							{
								radio_button(name: "$$$/Rasterize/Dlg/AutoOptions/RadioWhiteBG=&White",
									 identifier: "WhiteRadioButton",
									bind: @BackGroundRadioGp, value: 1);
								radio_button(name: "$$$/Rasterize/Dlg/AutoOptions/RadioTransparentBG=&Transparent",
									 identifier: "TransparentRadioButton",
									bind: @BackGroundRadioGp, value: 2);
							}
						}
					}
					group(name: "$$$/Rasterize/Dlg/AutoOptions/GroupOptions=Options",
							identifier: "OptionsGroup")
					{
						row(child_vertical: @align_top)
						{
							popup(identifier: "AntiAliasPopup",
								bind: @Antialias,
								name: "$$$/Rasterize/Dlg/Options/TextAntialiasing=&Anti-aliasing:",
								include_custom_item: false,
								items: [ { name: "$$$/Rasterize/Mnu/Antialiasing/None=None", value: 0 },
								{ name: "$$$/Rasterize/Mnu/Antialiasing/ArtOptimized=Art Optimized (Supersampling)", value: 1 },
								{ name: "$$$/Rasterize/Mnu/Antialiasing/TypeOptimized=Type Optimized (Hinted)", value: 2 } ] );
						overlay()
							{
								panel(bind: @Antialias,	value: 1, margin: 0)
								{
									info_icon( icontype: "note", size: "small",
									alt: "$$$/Rasterize/Str/ToolTip/ArtOptimizedAntialias=Text anti-aliasing settings will not be honored.");
								}
								panel(bind: @Antialias, value: 2, margin: 0)
								{
									info_icon( icontype: "note", size: "small",
									alt: "$$$/Rasterize/Str/ToolTip/TextOptimizedAntialias=Best suited for text Objects. Text anti-aliasing settings will be honored.");
								}	
							}
						}
						checkbox(name: "$$$/Rasterize/Dlg/AutoOptions/CheckCreateMask=Create Clipping &Mask",
                					 identifier: "CreateClippingMask",
               						 bind: @ClippingMaskCheckState, state: "unchecked", value_on: true, value_off: false,                				 guide_mask: [ @guide_label ]);

						unit_hot_number(identifier: "OptionsNumberEdit",
									name: "$$$/Rasterize/Dlg/AutoOptions/TextAdd=A&dd:",
									max_value: 720, min_value: 0,
									decimal_places: 2, digits: 5,
									unit: 'CurrentDocumentUnit',
									bind: @PaddingOptionsNumberEdit,
									stepper: true,
									suffix: "$$$/Rasterize/Dlg/AutoOptions/TextAroundObject=Around Object",
									guide_mask: [ @guide_label ]
									);	
						optional(bind: @IsRasterEffect, value: false, margin: 0)
						{
							checkbox(name: "$$$/Rasterize/Dlg/AutoOptions/CheckPreserveSpots=&Preserve spot colors",
									identifier: "PreserveSpotColors",
									bind: @PreserveSpotCheckboxState, state: "checked", value_on: true, value_off: false,
									guide_mask: [ @guide_label ]);
						}
					}
					optional(bind: @PreserveSpotWarning, value: 0, margin: 0)
					{
						row(horizontal: @align_fill)
						{			
							info_icon( icontype: "note", size: "small");
							static_text(characters: 1, horizontal: @align_fill,
										identifier: "PreserveSpotWarningString1",
										name: "$$$/Rasterize/Str/WarningPreserveSpot1=The spot colors will not be preserved during the rasterization.");
						}
					}
					optional(bind: @PreserveSpotWarning, value: 1, margin: 0)
					{
						row(horizontal: @align_fill)
						{
							info_icon( icontype: "note", size: "small");
							static_text(characters: 1, horizontal: @align_fill,
										identifier: "PreserveSpotWarningString2",
										name: "$$$/Rasterize/Str/WarningPreserveSpot2=The option to preserve spot colors is not currently checked in the Document Raster Effects Settings dialog.");
						}
					}
				}
			}	
		}			
		row(horizontal: @align_fill, child_horizontal: @align_right, reverse: os == @mac, spacing: @related_spacing)
		{
			button(name: "$$$/gOK=OK", identifier: "okButton", commit_button: true);
			button(name: "$$$/gCancel=Cancel", identifier: "cancelButton", commit_button: true);
		}
	}	
}

