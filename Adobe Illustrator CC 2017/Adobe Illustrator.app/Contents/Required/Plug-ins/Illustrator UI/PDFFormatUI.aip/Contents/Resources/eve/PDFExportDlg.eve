layout PDFExportDlg
{
	view subview(	identifier: "PDFExportDlg_subview",
					layout_theme: @dialog,
					child_horizontal: @align_fill,
					show_border: false,
					placement: @place_column
				)
	{				
		/*Below colum is just to mask guide & align two top popups properly.*/
		column(	guide_mask:		[ @guide_label ], spacing : 0)
		{
				overlay(horizontal:				@align_fill, child_horizontal:				@align_fill)
				{
					panel(	bind: @valueIsSavePDF,
							value: true,
							child_horizontal:				@align_fill)
					{
						row(horizontal:				@align_fill)
						{
							row(horizontal: @align_left)
							{
								popup(	identifier:				"PDFPresetPopup",
										name:					'$$$/PDFFormat/Static/Preset=&Adobe PDF Preset:',	
										set_default_focus:		true,
										include_custom_item:	false,	
										horizontal:				@align_fill,
										popup_maximum_width_chars: 32,
										popup_minimum_width_chars: 32,
										items:					[
																	{name:  '$$$/PDFFileFormat/Str/PDFStyleCustom=Custom', value: 0},
																	{name:  '', value: 1},
																	{ name: '$$$/PDFFileFormat/Str/PDFStyleAI=[Illustrator Default]', value: 2 }													
																]						
										);
							}
							row(horizontal: @align_right)
							{
								button ( identifier:			"SavePDFPresetBtn",
										 button_style:			true,									 
										 image:					{normal:"SP_Save_Md_N.png", dark_normal: "SP_Save_Md_N_D.png" },
										 action:				@SavePresetAction
										);
							}
						}			
					}
					panel(	bind: @valueIsSavePDF,
							value: false,
							child_horizontal: @align_fill,
							horizontal: @align_fill
						  )
					{
						edit_text(	identifier:			"PDFPresetEditText",										
						            name:				'$$$/PDFFormat/Static/Preset=&Adobe PDF Preset:',
						            bind:				@valuePDFPresetName,
						            set_default_focus:	true,
						            max_characters:		127
								  );								  							
						
					}
						
			}
			row(horizontal: @align_fill,child_horizontal: @align_fill)
				{
					row(child_horizontal: @align_fill )
					{
						popup(	identifier:				"StandardPopup",
								name:					'$$$/PDFFormat/Static/Standard=Sta&ndard:',
								include_custom_item:	false,																					
								items:					@standaradItems
							  );
					}		  
					 row(child_horizontal: @align_fill)
					{         
						 popup(	identifier:				"CompatibilityPopup",
								name:					'$$$/PDFFormat/Static/comp=&Compatibility:',
								include_custom_item:	false,															
								items:					@compatibilityItems			
							  );
					}
			}
		}
		row(horizontal: @align_fill)
		{
				list(   identifier: "ViewListBox", 						
						items:		@valueViewList,
						vertical:	@align_fill,
						bind:		@valuePanelView
					 );

				overlay(identifier: "ViewOverlay")
				{
/******************************************************************************** PANEL 1 -> General *********************************************************************/					
					panel(	identifier: "GeneralView", 
							bind:		@valuePanelView, 
							value:		1,							
							margin:		0,
							child_horizontal: @align_fill,
							guide_mask:		[ @guide_label,@guide_baseline ]
						  )
					{
						column(spacing: 0)
						{
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsGeneral=General',
										max_characters:	255 );
							separator(orientation:		@horizontal);
						}
						edit_text(	identifier:		"DescriptionEditText",										
						            name:			'$$$/PDFFormat/Static/desc=&Description:',
						            lines:			5,
						            scrollable:		true,
						            bind:			@valuePDFPresetDescription,
						            guide_mask:		[ @guide_label ],
									max_characters:	1024
								  );
					    group(	name:				'$$$/PDFFormat/Clusters/Options=Options',
								guide_mask:			[ @guide_label ]
							 )
						{
							checkbox(identifier:	"PreservePGFChkBx", 
									 name:			'$$$/PDFFormat/CheckBox/PGF=Preserve &Illustrator Editing Capabilities'
									 );
							checkbox(identifier:	"EmbedThumnailChkBx", 
									 name:			'$$$/PDFFormat/CheckBox/Thumbnail=&Embed Page Thumbnails',
									 bind:			@valueIsEmbedThumnail
									);
							checkbox(identifier:	"OptimizeForWebChkBx", 
									 name:			'$$$/PDFFormat/CheckBox/WebView=&Optimize for Fast Web View',
									 bind:			@valueIsOptimizeForWeb
									 );
							checkbox(identifier:	"ViewPDFChkBx",
									 name:			'$$$/PDFFormat/CheckBox/ViewAcrobat=&View PDF after Saving',										 
									 bind:			@valueIsViewPDF
									 );
							checkbox(identifier:	"CreateAcroLayersChkBx", 
									 name:			'$$$/PDFFormat/CheckBox/OCG=Create Acrobat &Layers from Top-Level Layers',
									 bind:			@valueIsCreateAcroLayers
									 );
						}					
					}
/******************************************************************************** PANEL 2 -> Compression *********************************************************************/
					panel(	identifier: "CompressionView", 
							bind:		@valuePanelView, 
							value:		2,							
							margin:		0,
							child_horizontal: @align_fill,
							guide_mask:		[ @guide_label,@guide_baseline ]
						  )
					{
						column(spacing: 0)
						{
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsCompression=Compression');
							separator(orientation:		@horizontal);
						}
						
						/******* Bitmap Image Group ********/
						
						group(	name:				'$$$/PDFFormat/Clusters/ColorImage=Color Bitmap Images',
								child_horizontal:	@align_left									
							 )
						{								
							row(child_horizontal: @align_fill, spacing: 0)
							{
								popup(	identifier:				"BmpSamplingPopup",										
										bind:					@valueBmpSamplingOption,
										include_custom_item:	false,											
										items:					@samplingItems
									  );
								edit_number(	identifier:		"BmpPPIEditNumber",
												suffix:			'$$$/PDFFormat/Static/Color/ImagesAbove=ppi for ima&ges above',
												min_value:		9,
												max_value:		2400,
												digits: 		2
										 );
								edit_number(	identifier:		"BmpPPIToleranceEditNumber",
												suffix:			'$$$/PDFFormat/Static/PPI=ppi',
												min_value: 		9,
												max_value: 		24000,
												digits:			2
										 );								
							}
							
							row(child_horizontal: @align_fill, spacing: 53)
							{
								popup(	identifier:			"BmpCompressionTypePopup",	
									name:					'$$$/PDFFormat/Static/Compression1=C&ompression:',	
									bind:					@valueBmpCompressionType,
									include_custom_item:	false,	
									guide_mask:				[ @guide_label ],										
									items:					@compressionTypeItems,
									popup_maximum_width_chars: 10, 
									popup_minimum_width_chars: 10
								  );
								edit_number(identifier:		"BmpTileSizeEditNumber",
										  name:				'$$$/PDFFormat/Static/Tile1=&Tile Size:',
										  bind:				@valueBmpTileSize,
										  guide_mask:		[ @guide_label ],
										  suffix:			'$$$/PDFFormat/Static/TileUnit1=pixels',
										  min_value:		128,
										  max_value:		2048,
										  digits: 			2
								);								
							}
							
								popup(	identifier:			"BmpQualityPopup",	
									name:					'$$$/PDFFormat/Static/Quality1=Image &Quality:',	
									include_custom_item:	false,	
									guide_mask:				[ @guide_label ],
									popup_maximum_width_chars: 5, 
									popup_minimum_width_chars: 5
								  );
																  
						}
						
						/******* Grayscale Image Group ********/
						
						group(	name:				'$$$/PDFFormat/Clusters/GrayscaleImage=Grayscale Bitmap Images',
								child_horizontal:	@align_left)
						{								
							row(child_horizontal: @align_fill, spacing: 0)
							{
								popup(	identifier:				"graySamplingPopup",										
										bind:					@valueGraySamplingOption,
										include_custom_item:	false,											
										items:					@samplingItems
									  );
								edit_number(	identifier:		"GrayPPIEditNumber",
												suffix:			'$$$/PDFFormat/Static/Grayscale/ImagesAbove=ppi for imag&es above',
												min_value:		9,
												max_value:		2400,
												digits: 		2
										 );
								edit_number(	identifier:		"GrayPPIToleranceEditNumber",
												suffix:			'$$$/PDFFormat/Static/PPI=ppi',
												min_value: 		9,
												max_value: 		24000,
												digits: 		2
										 );								
							}
							
							row(child_horizontal: @align_fill, spacing: 53)
							{
								popup(	identifier:			"GrayCompressionTypePopup",	
									name:					'$$$/PDFFormat/Static/Compression2=Compress&ion:',	
									bind:					@valueGrayCompressionType,
									include_custom_item:	false,	
									guide_mask:				[ @guide_label ],										
									items:					@compressionTypeItems,
									popup_maximum_width_chars: 10, 
									popup_minimum_width_chars: 10
								  );
								edit_number(identifier:		"GrayTileSizeEditNumber",
										  name:				'$$$/PDFFormat/Static/Tile2=Tile Si&ze:',
										  bind:				@valueGrayTileSize,
										  guide_mask:		[ @guide_label ],
										  suffix:			'$$$/PDFFormat/Static/TileUnit2=pixels',
										  min_value:		128,
										  max_value:		2048,
										  digits: 			2 
								);								
							}
							
							popup(	identifier:			"GrayQualityPopup",	
									name:					'$$$/PDFFormat/Static/Quality2=Image Q&uality:',	
									include_custom_item:	false,	
									guide_mask:				[ @guide_label ],
									popup_maximum_width_chars: 5, 
									popup_minimum_width_chars: 5
								  );						
						}
						/******* Monochrome Image Group ********/
						
						group(	name:				'$$$/PDFFormat/Clusters/Monochrome=Monochrome Bitmap Images',
								child_horizontal:	@align_left)
						{								
							row(child_horizontal: @align_left, spacing: 0)
							{
								popup(	identifier:				"MonoSamplingPopup",										
										bind:					@valueMonoSamplingOption,
										include_custom_item:	false,											
										items:					@samplingItems
									  );
								edit_number(	identifier:		"MonoPPIEditNumber",
												suffix:			'$$$/PDFFormat/Static/Monochrome/ImagesAbove=ppi for images a&bove',
												min_value:		9,
												max_value:		2400,
												digits: 		2
										 );
								edit_number(	identifier:		"MonoPPIToleranceEditNumber",
												suffix:			'$$$/PDFFormat/Static/PPI=ppi',
												min_value: 		9,
												max_value: 		24000,
												digits: 		2
										 );								
							}
							
							popup(	identifier:			"MonoCompressionTypePopup",	
									name:					'$$$/PDFFormat/Static/Compression3=Co&mpression:',
									bind:					@valueMonoCompressionType,	
									include_custom_item:	false,	
									guide_mask:				[ @guide_label ],										
									items:					@monoCompressionTypeItems,
									popup_maximum_width_chars: 10, 
									popup_minimum_width_chars: 10
								  );																																	
						}
						checkbox(identifier:		"CompressTextAndLineChkBx", 
									 name:			'$$$/PDFFormat/CheckBox/CompressArt=Compress Text and &Line Art',
									 bind:			@valueIsCompressTextAndLine,
									 guide_mask:	[ @guide_label ]
									 );
					}
/******************************************************************************** PANEL 3 -> Marks & Bleeds *********************************************************************/	
					panel(	identifier: "MarksAndBleedsView", 
							bind:		@valuePanelView, 
							value:		3,							
							margin:		0,
							child_horizontal: @align_fill,
							guide_mask:		[ @guide_label,@guide_baseline ]
						  )
					{
						column(spacing: 0)
						{
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsBleeds=Marks and Bleeds');
							separator(orientation:		@horizontal);
						}
						group(	name: '$$$/PDFFormat/Clusters/Marks=Marks',
								child_horizontal:	@align_fill								
							 )
						{
							checkbox(	identifier: "AllPrinterMarksChkBx", 
											name:		"$$$/PDFFormat/CheckBox/PrinterMNew=All Pr&inter's Marks",
											bind:		@valueIsAllPrinterMarks,
											value_off: 0,
											value_on: 1,											
											guide_mask:				[ @guide_label ]
											 );
							row(indent: gIndent)
							{								
								column(horizontal: @align_left)
								{
									checkbox(	identifier: "TrimMarksChkBx",			
												name: '$$$/PDFFormat/CheckBox/Trim=T&rim Marks',
												bind:		@valueIsTrimMarks
											);
									checkbox(	identifier: "RegistrationMarksChkBx", 
												name: '$$$/PDFFormat/CheckBox/Registration=Re&gistration Marks',
												bind:		@valueIsRegistrationMarks
											);
									checkbox(	identifier: "ColorBarsChkBx", 
												name: '$$$/PDFFormat/CheckBox/ColorBars=Co&lor Bars',
												bind:		@valueIsColorBars
											);
									checkbox(	identifier: "PageInformationChkBx", 
												name: '$$$/PDFFormat/CheckBox/PageInfo=Page Infor&mation',
												bind:		@valueIsPageInfo
											);
								}
								column(child_horizontal:	@align_fill)
								{
									popup(	identifier:			"PrinterMarkTypePopup",
											name:				'$$$/PDFFormat/Static/MarkType=Printer Mar&k Type:',
											include_custom_item: false,
											bind:				 @valuePrinterMarkType,
											items:				 @printerMarkTypeItems
										);
									popup(	identifier: "PrinterTrimMarkWeightPopup",
											name:		'$$$/PDFFormat/Static/MarkWeight=Trim Mark &Weight:',
											bind:		@valueTrimMarkWeight,
											include_custom_item: false,
											items:		@printerTrimMarkWeightItems
										);
									unit_hot_number(	identifier: "OffsetEditNumber",													
														name:		'$$$/PDFFormat/Static/Offset=O&ffset:',
														bind:		@valueOffset,
														stepper:	true,
														max_value: 72,
									                    min_value: 0,
									                    decimal_places: 3,
														unit:		"CurrentDocumentUnit",
														digits:		7
												);
								}				
							}																													
						}
						group(	name: '$$$/PDFFormat/Clusters/Bleeds=Bleeds',
								child_horizontal:	@align_fill,
								placement: @place_column								
							 )
						{
							
							checkbox(	identifier: "UseDocBleedChkBx", 
										name:		'$$$/Print/Options/DocumentBleed=Use Document Bleed Settings',
										bind:		@valueIsUseDocBleed,
										guide_mask:	[ @guide_label ]
										);
										
							row(child_horizontal: @align_center)
							{
								column()
								{								
									unit_hot_number(identifier: "TopEditNumber",
												name: '$$$/PDFFormat/Static/Top=T&op:',
												stepper:	true,
												unit:		"CurrentDocumentUnit",
												max_value: 72,
												decimal_places: 4,
									            min_value: 0,
												digits: 5
												);
									unit_hot_number(identifier: "BottomEditNumber", 											
												name: '$$$/PDFFormat/Static/Bottom=&Bottom:',
												stepper:	true,
												unit:		"CurrentDocumentUnit",
												max_value: 72,
												decimal_places: 4,
									            min_value: 0,
												digits: 5
												);				
									
								}	
								column(vertical: @align_fill)
								{									
									toggle_button( identifier:		"ConstraintToggleBtn",
												   bind:		@valueIsConstraintLock,
												   alt: "$$$/PDFFormat/Str/ToolTip/LinkItem=Constrain Width and Height Proportions",
												   image: {down: "SP_Link_Md_N.png", dark_down:"SP_Link_Md_N_D.png", normal: "SP_Unlink_Md_N.png", dark_normal:"SP_Unlink_Md_N_D.png"}, 
												   value_on: true, value_off: false,
												   vertical: @align_center,
												   wants_focus: false
   												  );									
								}
								column()
								{
									unit_hot_number(	identifier: "LeftEditNumber", 
														name: '$$$/PDFFormat/Static/Left=Lef&t:',
														stepper:	true,
														unit:		"CurrentDocumentUnit",
														max_value: 72,
														decimal_places: 4,
														min_value: 0,
														digits: 5
												);	
									unit_hot_number(	identifier: "RightEditNumber", 
														name: '$$$/PDFFormat/Static/Right=Rig&ht:',
														stepper:	true,
														unit:		"CurrentDocumentUnit",
														max_value: 72,
														decimal_places: 4,
														min_value: 0,
														digits: 5
												);											
								}
							}
							
						}					
					}
/******************************************************************************** PANEL 4 -> Output *********************************************************************/
					panel(	identifier: "OutputView", 
							bind:		@valuePanelView, 
							value:		4,							
							margin:		0,
							child_horizontal: @align_fill,
							guide_mask:		[ @guide_label,@guide_baseline ]
						  )
					{
						column(spacing: 0)
						{
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsOutput=Output');
							separator(orientation:		@horizontal);
						}
						
						group(	name:				'$$$/PDFFormat/Clusters/Color=Color',
								placement:			@place_column,
								child_horizontal:   @align_fill
								)
						{	
							
							row()
							{
								column(child_horizontal: @align_fill, horizontal: @align_fill)
								{
									popup(	identifier:				"ColorConversionPopup",
											name:					'$$$/PDFFormat/Static/ColorConversion=Color Con&version:',
											bind:					@valueColorConversionOption,
											items:					@colorConversionItems,
											include_custom_item:	false,
											popup_maximum_width_chars: 21,
											popup_minimum_width_chars: 21
										);
									
									popup(	identifier: 			"DestinationPopup",
											name:					'$$$/PDFFormat/Static/ColorDest=&Destination:',									
											include_custom_item:	false,
											popup_maximum_width_chars: 21,
											popup_minimum_width_chars: 21
											);
										
									popup(	identifier: 			"PIPolicyPopup",
											name:					'$$$/PDFFormat/Static/ColorPol=Profile &Inclusion Policy:',										
											include_custom_item:	false,
											popup_maximum_width_chars: 21,
											popup_minimum_width_chars: 21
											);
								}
								column(child_vertical: @align_top, horizontal: @align_right)
								{
									panel(bind: @valueIsColorConversionInfoIcon,
										  value: true,
										  margin:0)
									{									
										info_icon(	identifier:		"ColorConversionInfoIcon",
													icontype:		"warning", 
													size:			"small",
													vertical:		@align_top);									
									}
									panel(	bind:@valueDummyIcons,
											value: true,
											margin:0)
									{									
										info_icon(	icontype:		"warning", 
													size:			"small",
													vertical:		@align_top);
									}
									panel(	bind:@valueDummyIcons,
											value: true,
											margin:0)
									{									
										info_icon(	icontype:		"warning", 
													size:			"small",
													vertical:		@align_top);									
									}	
								}
							}														
						}
						group(	name:				'$$$/PDFFormat/Clusters/PDFX=PDF/X',
								child_horizontal:	@align_left,
								placement:			@place_column
							 )
						{	
							popup(	identifier: 		"OutputIntentProfileNamePopup",
									name:				'$$$/PDFFormat/Static/PDFX1=O&utput Intent Profile Name:',
									horizontal:			@align_fill,
									include_custom_item:	false,
									popup_maximum_width_chars: 21,
									popup_minimum_width_chars: 21
									);								
							
							edit_text(	identifier: 	"OutputConditionNameEditText",
										name:			'$$$/PDFFormat/Static/PDFX2=&Output Condition Name:',
										bind:			@valueOutputConditionName,
										horizontal:		@align_fill,
										max_characters:	255											
									);
							
							
							edit_text(	identifier: 	"OutputConditionIDEditText",
										name:			'$$$/PDFFormat/Static/PDFX3=Output Condition Id&entifier:',
										bind:			@valueOutputConditionID,
										horizontal:		@align_fill,
										max_characters:	255		
									);	
							
							edit_text(	identifier: 	"RegistryNameEditText",
										name:			'$$$/PDFFormat/Static/PDFX4=&Registry Name:',
										bind:			@valueRegistryName,
										horizontal:		@align_fill,
										max_characters:	255		
									);
							checkbox(	identifier:		"MarkTrappedChkBx",
										name:			'$$$/PDFFormat/CheckBox/Trap=Mark as &Trapped',
										bind:			@valueIsMarkTrapped
									);
						}
						group(	identifier: 	"DescriptionGroup",
								vertical: @align_fill,
								placement: @place_row, 
								header:'display_text(value:@valueOuputPanelHeaderText, horizontal: @align_left)'
							 )						
						{																
								optional(value: true, margin: 0, bind: @showWarningIcon,horizontal: @align_left)
								{
									
												info_icon(	identifier:		"WarningIcon",
												icontype:		"warning", 
												size:			"small");
										
								}
								static_text(horizontal: @align_fill,name: @valueOutputPanelDescStr, characters:1 );
						}
					}
/******************************************************************************** PANEL 5 -> Advanced *********************************************************************/
					panel(	identifier: "AdvancedView", 
							bind:		@valuePanelView, 
							value:		5,							
							margin:		0,
							child_horizontal: @align_fill,
							guide_mask:		[ @guide_label, @guide_baseline ]
						  )
					{
						column(spacing: 0)
						{
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsAdvanced=Advanced');
							separator(orientation:		@horizontal);
						}							
						
						group(	name:				'$$$/PDFFormat/Clusters/Fonts=Fonts',
								child_horizontal:	@align_left,
								placement:			@place_column								
							  )
							{
								edit_number(identifier: "CharacterUsed",
											name:		'$$$/PDFFormat/Static/Subset=Subset fonts &when percent of characters used is less than:',
											bind:		@valuePercentageCharUsedThreshold,
											unit:		"NumberEditUnit_Percent",
											max_value:	100,
											min_value:	0
										);	
								row(guide_mask:		[ @guide_label, @guide_baseline ])
								{		
									info_icon(	icontype:		"note", 
												size:			"small",
												vertical:		@align_top);
									static_text(name: '$$$/PDFFormat/Static/EmbedFontMsg=All fonts with appropriate embedding bits will be embedded.');
								}
							}
						group(	name:				'$$$/PDFFormat/Clusters/AdvOptions=Overprint and Transparency Flattener Options (PDF 1.3 Only)',
								placement:			@place_column,
								guide_mask:		[ @guide_label, @guide_baseline ]
							  )
							{
								popup(	identifier: 			"OverPrintsPopup",
										name:					'$$$/PDFFormat/Static/overprints=&Overprints:',
										bind:					@valueOverPrintsOption,
										popup_maximum_width_chars: 15,
										popup_minimum_width_chars: 15,
										items:					@overPrintsItems,
										include_custom_item:	false
										);
								row()
								{		
									popup(	identifier:				"FlattenerPresetPopup",
											name:					'$$$/PDFFormat/Static/preset=Prese&t:',
											popup_maximum_width_chars: 15,
											popup_minimum_width_chars: 15,
											include_custom_item:	false											
											);
									button(	identifier: "FlattenerPresetCustomBtn",
											name:		'$$$/PDFFormat/Buttons/Custom=Custo&m...',
											action:		@FlattenerCustomBtnAction											
										);
								}		
							}							
					}
/******************************************************************************** PANEL 5 -> Security *********************************************************************/
					panel(	identifier: "SecurityView", 
							bind:		@valuePanelView, 
							value:		6,							
							margin:		0,
							child_horizontal: @align_fill,
							guide_mask:		[ @guide_label, @guide_baseline ]
						  )
					{
						column(spacing: 0)
						{
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsSecurity=Security');
							separator(orientation:		@horizontal);
						}							
						row()
						{
							static_text(name: '$$$/PDFFormat/Static/EncryptLevel=Encryption Level: ');
							display_text(identifier: "EncryptionLevelDisplayText",
										 bind:		@valueEncryptionLevelStr,
										 horizontal: @align_left
									   );
						}
						checkbox(	identifier:  "RequireOpenPwdChkBx", 
									name:		 '$$$/PDFFormat/CheckBox/ReqDocPass=Re&quire a password to open the document',
									bind:		  @valueIsRequireOpenPwd,
									guide_mask:	  [ @guide_label, @guide_baseline ]
								  );
						/*to override Parent's align_fill.  which was for separator*/
						row()
						{		  
							edit_text(	identifier: "OpenPasswordEditText", 										
										name:		'$$$/PDFFormat/Static/DocPass=&Document Open Password: ',					
										password:	true,
										max_characters:	32,
										characters:		19
									);		  						
						}		  
						row()
						{
							info_icon(	icontype:		"note", 
										size:			"small",
										vertical:		@align_top);
							static_text(name: '$$$/PDFFormat/Str/DocPassMsg=When set, this password is required to open the document.');
						}
						
						checkbox(	identifier:  "RequirePermissionPwdChkBx", 
									name:		'$$$/PDFFormat/CheckBox/UseDocPass=&Use a password to restrict editing Security and Permissions settings',
									bind:		 @valueIsRequirePermissionPwd,
									guide_mask:	 [ @guide_label, @guide_baseline ]
								  );
						
						/*to override Parent's align_fill.  which was for separator*/
						row()
						{
							edit_text(	identifier: "PermissionPasswordEditText", 
										name:		'$$$/PDFFormat/Static/PerPass=Permissions Pass&word: ',
										password: true,
										max_characters:	32,
										characters:		19
									);		  						
						}			
						
						row()
						{
							info_icon(	icontype:		"note", 
										size:			"small",
										vertical:		@align_top);
							static_text(name: '$$$/PDFFormat/Str/PermPassMsg=This password is required to open the document in PDF editing applications.',
										guide_mask:		[ @guide_label, @guide_baseline ]
										);
						}
						
						group(	name: '$$$/PDFFormat/Clusters/Perms=Acrobat Permissions', 
								placement: @place_column, 
								vertical: @align_top,
								guide_mask:		[ @guide_label, @guide_baseline ],
								horizontal: @align_fill
								)
						{
							popup(	identifier:	"PrintingAllowedPopup",
									name:		'$$$/PDFFormat/Static/Printing=Printing Allow&ed:',	
									horizontal:	@align_fill,
									include_custom_item:		false
								  );
							popup(	identifier:	"ChangesAllowedPopup",
									name:		'$$$/PDFFormat/Static/ChangeAllowed=Chan&ges Allowed:',	
									horizontal:	@align_fill,
									include_custom_item:		false									
								  );
							overlay(guide_mask:		[ @guide_label, @guide_baseline])
							{
								panel(value: 0,
									  bind: @valueOuterPanel,
									  margin: 0)
								{
								   checkbox(identifier: "EnableCopyingForVisuallyImpairedChkBx",
											name: '$$$/PDFFormat/CheckBox/AllowContent40FullText=Enable cop&ying of text, images, and other content and access for the visually impaired',
											bind:		@valueIsEnableCopyingForVisuallyImpaired,
											guide_mask:		[ @guide_label, @guide_baseline ]);
								}
								panel(	value: 1,
										bind: @valueOuterPanel,
										margin: 0)
								{
									checkbox(	identifier:  "EnableCopyingChkBx", 
												name: '$$$/PDFFormat/CheckBox/AllowContent=Enable cop&ying of text, images, and other content',
												bind:		@valueIsEnableCopying,
												guide_mask:		[ @guide_label, @guide_baseline ]
									);
								}
							}	  							
							
								panel(	identifier: 		"PDF1_4View", 
										bind:				@valueOuterPanel, 
										value:				1,							
										margin:				0,
										child_horizontal:	@align_fill,
										guide_mask:			[ @guide_label,@guide_baseline ]
									  )
								{									
									checkbox(	identifier:  "EnableTextAccessChkBx", 
											name:		'$$$/PDFFormat/CheckBox/EnableContent=Enable text access of screen reader devices for the &visually impaired',
											bind:		@valueIsEnableTextAccess,
											guide_mask:	[ @guide_label, @guide_baseline ]
									  );
									  
									panel(	identifier: "PDF1_567View", 
											bind:		@valueInnerPanel, 
											value:		2,							
											margin:		0,
											child_horizontal: @align_fill,
											guide_mask:		[ @guide_label,@guide_baseline ]
										)
									{	  	  								  
										checkbox(	identifier:  "EnableMetadatChkBx", 
													name: '$$$/PDFFormat/CheckBox/TextMeta=Enable plaintext &metadata',
													bind:		@valueIsEnableMetadata,
													guide_mask:		[ @guide_label, @guide_baseline ]
												);
									}												    
								}																									  	  
						}
					}
/******************************************************************************** PANEL 5 -> Summary *********************************************************************/
					panel(	identifier: "SummaryView", 
							bind:		@valuePanelView, 
							value:		7,							
							margin:		0,
							child_horizontal: @align_fill,
							child_vertical: @align_fill,
							guide_mask:		[ @guide_label, @guide_baseline ]
						  )
					{
						column(spacing: 0)
						{							
							static_text(name:			'$$$/PDFFormat/Static/PDFPanelsSummary=Summary');
							separator(orientation:		@horizontal);
						}
						
						row()
						{
							overlay(guide_mask:		[ @guide_label, @guide_baseline])
							{
								/*Hack For aligning Options & Warning labels*/
								panel(value: 0,
									  bind: @valuePanelView,
									  margin: 0)
								{
								  static_text( name: '$$$/PDFFormat/Static/Warn=Warnings:');
								}
								panel(	value: 7,
										bind: @valuePanelView,
										margin: 0)
								{
									static_text( name: '$$$/PDFFormat/Static/Opt=Options:');
								}
							}				
							
							multicolumn_tree(	identifier: "SummaryMCT",										 
												wants_focus: false,
												horizontal: @align_fill,
												height:		275,
												
												items:	[
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/PDFStyle=Adobe PDF Preset:",
																value: @valuePDFPresetSummaryString
															},
															{
															  name:"$$$/PDFFormatUI/SummaryTree/Str/PDFStyleCompatibility=Compatibility:",
																value: @valuePDFCompatibilitySummaryString
															},
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/Summary/Standard=Standard:",
																value: @valuePDFStandardSummaryString
															},
															{
																name:""
															},
															{
																name:"$$$/PDFFormatUI/SummaryTree/Clusters/OutputDesc=Description"
															},
															{
																name:"$$$/PDFFormat/Static/PDFPanelsGeneral=General",
																children:   [
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStylePGF=Preserve Illustrator Editing Capabilities:", value: @valueIsPreservePGF, hide: @valueIsPreservePGFIsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleThumbnails=Embed Page Thumbnails:", value: @valueIsEmbedThumnail},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleViewPDF=View PDF File after Saving:", value: @valueIsViewPDF, hide: @valueIsViewPDFIsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleFastWebViewStr=Optimize for Fast Web View:", value: @valueIsOptimizeForWeb},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleAcrobatLayers=Create Acrobat Layers from Top-Level Layers:", value: @valueIsCreateAcroLayers, hide: @valueIsCreateAcroLayersIsDisabled}
																			]
															},
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/PDFStyleComp=Compression",
																children:   [
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleColorBitmapImage=Color Bitmap Images:", 
																				 children: 
																						[
																							{name: "", value: @valueBmpSamplingOptionSummaryString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleResolution=Resolution:", value: @valueBmpPPI, hide : @valueBmpPPIIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleForImageAbove=For Image Above:", value: @valueBmpPPITolerance, hide : @valueBmpPPIToleranceIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleComp=Compression:", value: @valueBmpCompressionTypeString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/quality=Image Quality:", value: @valueBmpImageQualitySummaryString, hide : @valueBmpImageQualityIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleTile=Tile Size:", value: @valueBmpTileSize, hide : @valueBmpTileSizeIsDiasbled}
																						]
																				},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleGrayBitmapImage=Grayscale Bitmap Images:", 
																				 children: 
																						[
																							{name: "", value: @valueGraySamplingOptionSummaryString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleResolution=Resolution:", value: @valueGrayPPI, hide : @valueGrayPPIIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleForImageAbove=For Image Above:", value: @valueGrayPPITolerance, hide : @valueGrayPPIToleranceIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleComp=Compression:", value: @valueGrayCompressionTypeString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/quality=Image Quality:", value: @valueGrayImageQualitySummaryString, hide : @valueGrayImageQualityIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleTile=Tile Size:", value: @valueGrayTileSize, hide : @valueGrayTileSizeIsDisabled}
																						]
																				},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleMonoBitmapImage=Monochrome Bitmap Images:", 
																				 children: 
																						[
																							{name: "", value: @valueMonoSamplingOptionSummaryString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleResolution=Resolution:", value: @valueMonoPPI, hide : @valueMonoPPIIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleForImageAbove=For Image Above:", value: @valueMonoPPITolerance, hide : @valueMonoPPIToleranceIsDisabled},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleComp=Compression:", value: @valueMonoCompressionTypeSummaryString}																							
																						]
																				},
																				{
																					name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleCompressArt=Compress Text and Line Art:",
																					value: @valueIsCompressTextAndLine
																				}																				
																			]
															},															
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/PDFPanelsBleeds=Marks and Bleeds",
																children:   [
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/TrimMarks=Trim Marks:", value: @valueIsTrimMarks},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/RegMarks=Registration Marks:", value: @valueIsRegistrationMarks},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/ColorBars=Color Bars:", value: @valueIsColorBars}	,
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/PageInformation=Page Information:", value: @valueIsPageInfo},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/Type=Printer Mark Type:", value: @valuePrinterMarkTypeString},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/Weight=Trim Mark Weight:", value: @valueTrimMarkWeightString},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/MarksBleeds/Offset=Offset from Artboard:", value: @valueOffsetStr, hide: @valueOffsetIsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleDocBleed=Use Document Bleed:", value: @valueIsUseDocBleed, hide: @valueBleedsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleBleedLeft=Bleed Left:", value: @valueBleedLeftStr, hide: @valueIsUseDocBleed},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleBleed/Right=Bleed Right:", value: @valueBleedRightStr, hide: @valueIsUseDocBleed},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleBleedTop=Bleed Top:", value: @valueBleedTopStr, hide: @valueIsUseDocBleed},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleBleedBottom=Bleed Bottom:", value: @valueBleedBottomStr, hide: @valueIsUseDocBleed}
																			]
															},
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/PDFPanelsOutput=Output",
																children:   [
																				{name: "$$$/PDFormatUI/SummaryTree/Str/SummaryTree/Color=Color:", 
																				 children: 
																						[
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/ColorConversion=Color Conversion:", value: @valueColorConversionOptionSummaryString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/Destination=Destination:", value: @valueDestinationOptionString, hide:@valueIsDestinationOption},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/ProfileInclusion=Profile Inclusion Policy:", value: @valueProfileInclusionPolicySummaryString}																							
																						]
																				},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/SummaryTree/PDFX=PDF/X:", hide: @valueIsPDFXIsDisabled,
																				 children: 
																						[
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/OutputIntentProfileName=Output Intent Profile Name:", value: @valueOutputIntentProfileName},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/OutputCondition=Output Condition:", value: @valueOutputConditionName, hide:@valueIsOutputConditionName},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/OutputConditionIdentifier=Output Condition Identifier:", value: @valueOutputConditionID},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/RegistryName=Registry Name:", value: @valueRegistryName},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/Summary/Trapped=Mark as Trapped:", value: @valueIsMarkTrapped}
																						]
																				}																																							
																			]
															},
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/PDFPanelsAdvanced=Advanced",
																children:   [
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleSubsetFontBelow=Subset embedded fonts below:", 
																				 value: @valuePercentageCharUsedThresholdStr																						
																				},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleOverprint=Overprint:", 
																				 value: @valueOverPrintsOptionSummaryString,
																				 hide: @valueOverPrintsOptionIsDisabled
																				},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleTransparencyStyle=Flattener Preset:", 
																				 value: @valueFlattenerPresetSummaryString,
																				 hide: @valueFlattenerPresetIsDisabled,
																				 children: 
																						[
																							{name: "$$$/PDFormatUI/SummaryTree/Str/FlatteningBalance=Raster/Vector Balance:", value: @valueRasterVectorBalance},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/MaxRes=Line Art and Text Resolution:", value: @valueLineArtAndTextResolutionString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/MinRes=Gradient and Mesh Resolution:", value: @valueGradientAndMeshResolutionString},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/OutlineText=Convert All Text to Outlines:", value: @valueIsConvertTextToOutline},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/OutlineStrokes=Convert All Strokes to Outlines:", value: @valueIsConvertStrokesToOutline},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/ClipComplex=Clip Complex Regions:", value: @valueIsClipComplexRegions},
																							{name: "$$$/PDFormatUI/SummaryTree/Str/AntiAlias=Anti-alias Rasters:", value: @valueIsAntiAlias}
																						]
																				}																																							
																			]
															},
															{
																name:"$$$/PDFFormatUI/SummaryTree/Str/PDFPanelsSecurity=Security", hide : @valueIsNotSavePDF,
																children:   [
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleUserPassword=Document Open Password:", value: @valueIsRequireOpenPwd},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleMasterPassword=Permissions Password:", value: @valueIsRequirePermissionPwd},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleEncryptionLevel=Encryption Level:", value: @valueEncryptionLevelStr}	,
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleChangesAllowed=Changes Allowed:", value: @valueChangesAllowedOptionString, hide: @valueAcrobatPermissionsIsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStylePrinting=Printing Allowed:", value: @valuePrintingAllowedOptionString, hide: @valueAcrobatPermissionsIsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleAllowContent=Enable copying of text, images, and other content:", value: @valueIsEnableCopying, hide: @valueAcrobatPermissionsIsDisabled},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStyleEnableContentAccess=Enable text access of screen reader devices:", value: @valueIsEnableTextAccess, hide: @valueIsEnableTextAccessHidden},
																				{name: "$$$/PDFormatUI/SummaryTree/Str/PDFStylePlaintextMetadata=Enable plaintext metadata:", value: @valueIsEnableMetadata, hide: @valueIsEnableMetadataHidden}
																			]
															}
														]
											 );
                         }
                         panel(bind:  @valueIsSavePDF,
							  value: true,
							  child_horizontal: @align_fill,
							  child_vertical:@align_fill,
							  margin:0)
						{
							row(child_vertical: @align_fill, child_horizontal: @align_fill)
							{
								static_text( name: '$$$/PDFFormat/Static/Warn=Warnings:', horizontal: @align_left);	
								
								multicolumn_tree(	identifier: "WarningMCT",
													wants_focus: false,
													horizontal: @align_fill,
													height: 85);																	
																										
							}				
						}					
                         	button( identifier: "SaveSummaryBtn",
								 name: '$$$/PDFFormat/Buttons/summary=Save Summar&y...',
								 horizontal: @align_left,
								 vertical: @align_bottom,
								 action:@SaveSummaryAction);
						  
					}
/******************************************************************************** End Overlay Panels  *********************************************************************/
				}
		}
		row(child_horizontal: @align_right, spacing: @related_spacing, reverse: os == @mac)
		{
			overlay(margin: 0,spacing: 0)
			{
				panel(bind: @valueIsSavePDF, 
				      value: true, 
					  margin: 0,
					  spacing: 0)
				 {
					button(	identifier: "savePDFButton",
					name: '$$$/PDFFormat/Buttons/Save=&Save PDF',
					commit_button: true,
					action:@SaveNewOrEditPDFFileAction);
				}
				panel(bind: @valueIsSavePDF, 
				      value: false,
					  margin: 0,
					  spacing: 0)
				 {
					button(	identifier: "okButton",
					name: '$$$/gOK=OK',
					commit_button: true,
					set_default_focus: true);
				}			
			}
			overlay(margin: 0,spacing: 0)
			{
				panel(bind: @valueIsSavePDF,value: true,margin: 0,spacing: 0)
				{
					button(identifier:"cancel_ResetButton",
					items:[
							{name: '$$$/gCancel=Cancel', action:@CancelAction},
							{name: '$$$/PDFFormat/Buttons/Reset=Reset', action:@ResetSettingsAction, modifiers:@opt}
					],
					commit_button: true);
				}
				panel(bind: @valueIsSavePDF,value: false,margin: 0,spacing: 0)
				{
					button(	identifier: "cancelButton",
					name: '$$$/gCancel=Cancel',
					commit_button: true);
				}		
			}
		}  						  		  		
	} 
} 
